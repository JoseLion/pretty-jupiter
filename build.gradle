plugins {
  id 'codenarc'
  id 'groovy'
  id 'jacoco'
  id 'java-gradle-plugin'

  id 'com.gradle.plugin-publish' version '0.10.1'
  id 'nebula.lint' version '16.2.3'
}

version = '1.0.0'

dependencyLocking {
  lockAllConfigurations()
}

codenarc {
  reportFormat('console')
  toolVersion('1.5')
}

gradleLint {
  alwaysRun = false

  rules += [
    'all-dependency',
    'all-nebula-renames',
    'archaic-wrapper',
    'duplicate-dependency-class'
  ]
}

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  implementation localGroovy()

  testImplementation(group: 'org.codehaus.groovy', name: 'groovy', version: '2.5.4')
  testImplementation(group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5')
}

pluginBundle {
  website = 'https://github.com/JoseLion/pretty-jupiter'
  vcsUrl = 'https://github.com/JoseLion/pretty-jupiter.git'
  tags = ['junit', 'jupiter', 'logger', 'pretty', 'mocha']
}

gradlePlugin {
  plugins {
    prettyJupiter {
      id = 'com.github.joselion.pretty-jupiter'
      implementationClass = 'com.github.joselion.prettyjupiter.PrettyJupiterPlugin'
      displayName = 'Pretty Jupiter Plugin'
      description = 'Gradle plugin to log JUnit Jupiter tests with a pretty Mocha.js style'
    }
  }
}

test {
  testLogging {
    showStandardStreams = true
    events('passed', 'skipped', 'failed')
  }
}

jacocoTestReport {
  reports {
    html.enabled(true)
    xml.enabled(true)
  }
}
