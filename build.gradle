plugins {
  id 'codenarc'
  id 'groovy'
  id 'java-gradle-plugin'

  id 'nebula.lint' version '16.2.3'
}

dependencyLocking {
  lockAllConfigurations()
}

codenarc {
  reportFormat('console')
  toolVersion('1.5')
}

gradleLint {
  rules += [
    'all-dependency',
    'all-nebula-renames',
    'archaic-wrapper',
    'duplicate-dependency-class'
  ]
}

sourceSets {
  functionalTest {
  }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  testImplementation(group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5')
  testImplementation(group: 'org.codehaus.groovy', name: 'groovy', version: '2.5.4')
  
  functionalTestImplementation(group: 'org.codehaus.groovy', name: 'groovy', version: '2.5.4')
}

gradlePlugin {
  plugins {
    greeting {
      id = 'com.github.joselion.pretty-jupiter'
      implementationClass = 'com.github.joselion.prettyjupiter.PrettyJupiterPlugin'
    }
  }
}

task functionalTest(type: Test) {
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
  dependsOn(tasks.functionalTest)
}
