buildscript {
  configurations.classpath {
    resolutionStrategy.activateDependencyLocking()
  }
}

plugins {
  id 'codenarc'
  id 'groovy'
  id 'jacoco'
  id 'java-gradle-plugin'

  id 'com.gradle.plugin-publish' version '1.2.1'
}

group = 'io.github.joselion'

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(GroovyCompile) {
  groovyOptions.encoding = 'UTF-8'
}

dependencyLocking {
  lockAllConfigurations()
}

codenarc {
  reportFormat('console')
  toolVersion('3.3.0')
}

jacoco {
  toolVersion('0.8.10')
}

repositories {
  mavenCentral()
}

dependencies {
  implementation(localGroovy())

  testImplementation(gradleTestKit())
  testRuntimeOnly('net.bytebuddy:byte-buddy:1.14.7')
  testRuntimeOnly('org.objenesis:objenesis:3.3')
}

testing {
  suites {
    def applySpock = { suite ->
      suite.useSpock('2.4-M1-groovy-3.0')

      suite.targets {
        all {
          testTask.configure {
            testLogging {
              showStandardStreams(true)
              events('passed', 'skipped', 'failed')
            }
          }
        }
      }
    }

    test(applySpock)
    e2e(JvmTestSuite, applySpock)
  }
}

tasks.named('check') {
  dependsOn(testing.suites.e2e)
}

jacocoTestReport {
  dependsOn(test, e2e)

  reports {
    html.required = true
    xml.required = true
  }
}

gradlePlugin {
  website = 'https://joselion.github.io/pretty-jupiter/'
  vcsUrl = 'https://github.com/JoseLion/pretty-jupiter'

  plugins {
    prettyJupiter {
      id = 'io.github.joselion.pretty-jupiter'
      implementationClass = 'io.github.joselion.prettyjupiter.PrettyJupiterPlugin'
      displayName = 'Pretty Jupiter Plugin'
      description = 'Gradle plugin to log JUnit Jupiter tests with a pretty Mocha.js style'
      tags.set([
        'junit',
        'jupiter',
        'junit5',
        'logger',
        'pretty',
        'tdd',
        'bdd'
      ])
    }
  }

  testSourceSets(sourceSets.e2e)
}
