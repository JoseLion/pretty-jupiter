plugins {
  id 'codenarc'
  id 'groovy'
  id 'jacoco'
  id 'java-gradle-plugin'

  id 'com.gradle.plugin-publish' version '0.12.0'
  id 'nebula.lint' version '16.17.0'
}

version = '1.3.1'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencyLocking {
  lockAllConfigurations()
}

codenarc {
  reportFormat('console')
  toolVersion('2.0.0')
}

gradleLint {
  alwaysRun = false

  rules += [
    'all-dependency',
    'all-nebula-renames',
    'archaic-wrapper',
    'duplicate-dependency-class'
  ]
}

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  testImplementation(group: 'org.codehaus.groovy', name: 'groovy', version: '3.0.7', ext: 'pom')
  testImplementation(group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5')
  testRuntimeOnly(group: 'net.bytebuddy', name: 'byte-buddy', version: '1.10.20')
  testRuntimeOnly(group: 'org.objenesis', name: 'objenesis', version: '3.1')
}

pluginBundle {
  website = 'https://github.com/JoseLion/pretty-jupiter'
  vcsUrl = 'https://github.com/JoseLion/pretty-jupiter.git'
  tags = ['junit', 'jupiter', 'logger', 'pretty', 'mocha']
}

gradlePlugin {
  plugins {
    prettyJupiter {
      id = 'com.github.joselion.pretty-jupiter'
      implementationClass = 'com.github.joselion.prettyjupiter.PrettyJupiterPlugin'
      displayName = 'Pretty Jupiter Plugin'
      description = 'Gradle plugin to log JUnit Jupiter tests with a pretty Mocha.js style'
    }
  }
}

sourceSets {
  e2e {
  }
}

gradlePlugin.testSourceSets(sourceSets.e2e)
configurations.e2eImplementation.extendsFrom(configurations.testImplementation)

tasks.register('e2e', Test) {
  testClassesDirs = sourceSets.e2e.output.classesDirs
  classpath = sourceSets.e2e.runtimeClasspath
}

tasks.named('test') {
  finalizedBy(tasks.e2e)
}

test {
  testLogging {
    showStandardStreams = true
    events('passed', 'skipped', 'failed')
  }
}

jacocoTestReport {
  reports {
    html.enabled(true)
    xml.enabled(true)
  }
}
